
# Rules
  $top          <- WS lines WS
 *lines         <- line WS_ (line_sep WS line)*
 *line          <- %comment / rule

  comment       <- "#" (!"\n" .)*
  rule          <- lefthand WS_ %"<-" WS_ r_any

# third-level
  r_any         <- r_sequence (WS %"/" WS r_sequence)*
  r_sequence    <- r_secondary (WS_ r_secondary)*

# second-level
  r_repeat      <- r_primitive ("*" / "?")
  r_prefix      <- ("!" / "&" / "%") r_primitive

 *r_secondary   <- r_repeat / r_prefix / r_primitive

# first-level
  nonterminal   <- identifier
  charrange     <- %"[" C_any %"-" C_any %"]"
  terminal      <- %'"' C_any (!'"' C_any)* %'"'
                 / %"'" C_any (!"'" C_any)* %"'"
  any-character <- %"."

 *r_primitive   <- nonterminal / charrange / terminal / any-character
                 / %"(" r_any %")"

# Various
  identifier    <- C_letter C_letter*
  lefthand      <- "*"? WS_ identifier

 *line_sep      <- %"\n"
# *line_sep      <- %((WS_ C_eol WS_)*)
 *WS            <- %((" " / "\t" / "\r" / "\n")*)
 *WS_           <- %((" " / "\t" / "\r")*)

# Characters
 *C_letter      <- [A-Z] / [a-z] / "_" / "-" / "$"
 *C_any         <- .
